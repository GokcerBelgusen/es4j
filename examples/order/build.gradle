plugins {
    id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: 'java'
apply plugin: 'idea'

repositories {
    jcenter()
    mavenLocal()
}

idea {
    project {
        languageLevel = '1.8'
        vcs = 'Git'
        ipr.withXml { xmlFile ->
            // enable 'Annotation Processors'
            xmlFile.asNode().component.find {
                it.@name == 'CompilerConfiguration'
            }['annotationProcessing'][0].replaceNode {
                annotationProcessing {
                    profile(default: true, name: 'Default', useClasspath: 'true', enabled: true)
                }
            }
        }
    }
}

license {
    header project.file('../../LICENSE-HEADER')
}

Properties props = new Properties()
props.load(new FileInputStream("../../gradle.properties"))
def eventsourcingVersion = props.get("version")

dependencies {
    testCompile 'org.testng:testng:6.9.10'

    compile 'org.projectlombok:lombok:1.16.8'

    compile "com.eventsourcing:eventsourcing-core:${eventsourcingVersion}"
    compile "com.eventsourcing:eventsourcing-h2:${eventsourcingVersion}"
}

test.useTestNG()
